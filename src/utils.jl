
const DEBUG = Preferences.@load_preference("use_debug", false)

"""
    debug_mode(; enable=true)

Helper to turn on (`enable=true`) or off (`enable=false`) debug expressions in Ferrite.

Debug mode influences `Ferrite.@debug expr`: when debug mode is enabled, `expr` is
evaluated, and when debug mode is disabled `expr` is ignored.
"""
function debug_mode(; enable = true)
    if DEBUG == enable == true
        @info "Debug mode already enabled."
    elseif DEBUG == enable == false
        @info "Debug mode already disabled."
    else
        Preferences.@set_preferences!("use_debug" => enable)
        @info "Debug mode $(enable ? "en" : "dis")abled. Restart the Julia session for this change to take effect!"
    end
    return
end

@static if DEBUG
    @eval begin
        macro debugonly(ex)
            return :($(esc(ex)))
        end
    end
else
    @eval begin
        macro debugonly(ex)
            return nothing
        end
    end
end

# TODO remove these once they are merged
module FerriteUtils
using Ferrite
import Adapt

include("ferrite-addons/PR883.jl")
include("ferrite-addons/gpu/adapt.jl")

end

include("ferrite-addons/collections.jl")
include("ferrite-addons/quadrature_iterator.jl")


function celldofsview(dh::Ferrite.AbstractDofHandler, i::Integer)
    ndofs = ndofs_per_cell(dh, i)
    offset = dh.cell_dofs_offset[i]
    return @views dh.cell_dofs[offset:(offset+ndofs-1)]
end

@inline angle(v1::Vec{dim, T}, v2::Vec{dim, T}) where {dim, T} = acos((v1 ⋅ v2)/(norm(v1)*norm(v2)))
@inline angle_deg(v1::Vec{dim, T}, v2::Vec{dim, T}) where {dim, T} = rad2deg(angle(v1, v2))

"""
    normalize(v::Ferrite.Vec)

Compute the normalized vector.
"""
function normalize(v::Ferrite.Vec)
    all(isapprox.(v, 0.0)) && return zero(v)
    return v / norm(v)
end

"""
    unproject(v::Vec{dim,T}, n::Vec{dim,T}, α::T)::Vec{dim, T}

Unproject the vector `v` from the plane with normal `n` such that the angle between `v` and the
resulting vector is `α` (given in radians).

!!! note It is assumed that the vectors are normalized and orthogonal, i.e. `||v|| = 1`, `||n|| = 1`
         and `v \\cdot n = 0`.
"""
@inline function unproject(v::Vec{dim, T}, n::Vec{dim, T}, α::T)::Vec{dim, T} where {dim, T}
    @debugonly @assert norm(v) ≈ 1.0
    @debugonly @assert norm(n) ≈ 1.0
    @debugonly @assert v ⋅ n ≈ 0.0

    α ≈ π/2.0 && return n # special case to prevent division by 0

    λ = (sqrt(1-cos(α)^2))/cos(α)
    return v + λ * n
end

"""
    rotate_around(v::Vec{dim,T}, a::Vec{dim,T}, θ::T)::Vec{dim,T}

Perform a Rodrigues' rotation of the vector `v` around the axis `a` with `θ` radians.

!!! note It is assumed that the vectors are normalized, i.e. `||v|| = 1` and `||a|| = 1`.
"""
@inline function rotate_around(v::Vec{dim, T}, a::Vec{dim, T}, θ::T)::Vec{dim, T} where {dim, T}
    @debugonly @assert norm(n) ≈ 1.0

    return v * cos(θ) + (a × v) * sin(θ) + a * (a ⋅ v) * (1-cos(θ))
end

"""
    orthogonalize(v₁::Vec{dim,T}, v₂::Vec{dim,T})::Vec{dim,T}

Returns a new `v₁` which is orthogonal to `v₂`.
"""
@inline function orthogonalize(v₁::Vec{dim, T}, v₂::Vec{dim, T})::Vec{dim, T} where {dim, T}
    return v₁ - (v₁ ⋅ v₂)*v₂
end

"""
    orthogonalize_normal_system(v₁::Vec{dim,T}, v₂::Vec{dim,T})

Returns new vectors which are orthogonal to each other.
"""
@inline function orthogonalize_normal_system(v₁::Vec{2, T}, v₂::Vec{2, T}) where {T}
    w₁ = v₁
    w₂ = v₂ - (w₁ ⋅ v₂)*w₁
    return w₁, w₂
end

orthogonalize_system(v₁::Vec{2}, v₂::Vec{2}) = orthogonalize_normal_system(v₁/norm(v₁), v₂/norm(v₂))

"""
    orthogonalize_normal_system(v₁::Vec{3}, v₂::Vec{3}, v₃::Vec{3})

Returns new vectors which are orthogonal to each other.
"""
@inline function orthogonalize_normal_system(v₁::Vec{3}, v₂::Vec{3}, v₃::Vec{3})
    w₁ = v₁
    w₂ = v₂ - (w₁ ⋅ v₂)*w₁
    w₃ = v₃ - (w₁ ⋅ v₃)*w₁ - (w₂ ⋅ v₃)*w₂
    return w₁, w₂, w₃
end

orthogonalize_system(v₁::Vec{3}, v₂::Vec{3}, v₃::Vec{3}) =
    orthogonalize_normal_system(v₁/norm(v₁), v₂/norm(v₂), v₃/norm(v₃))

"""
    compute_relative_rotation(v_from_in, v_to, n)

Relative rotation of `v_from_in` onto `v_to` about `n`, using the left hand rule. `v_from_in` is
folded onto the acute side first, so the magnitude is at most 90° and a sign flip of the reference
direction does not change the result.

The magnitude comes from `atan(‖a×b‖, a⋅b)` rather than `acos(a⋅b)`. A previous formulation clamped
the dot product to ±0.9999 before `acos`, which imposed a hard floor of `acos(0.9999) = 0.8103°` on
every returned magnitude: near-parallel vectors came back as ±0.81° with essentially arbitrary sign.
That is harmless for angles of tens of degrees but destroys anything of order 1°. `atan` is exact,
well conditioned at both 0 and π, needs no clamp, and is insensitive to the inputs not being exactly
normalized.
"""
function compute_relative_rotation(v_from_in::Vec{3}, v_to::Vec{3}, n::Vec{3})
    v_from = sign(v_from_in ⋅ v_to) * v_from_in
    axb = v_from × v_to
    return sign(axb ⋅ n) * atan(norm(axb), v_from ⋅ v_to)
end

"""
    ThreadedSparseMatrixCSR
Threaded version of SparseMatrixCSR.

Based on https://github.com/BacAmorim/ThreadedSparseCSR.jl .
"""
struct ThreadedSparseMatrixCSR{Tv, Ti <: Integer} <: AbstractSparseMatrix{Tv, Ti}
    A::SparseMatrixCSR{1, Tv, Ti}
end

function ThreadedSparseMatrixCSR(
    m::Integer,
    n::Integer,
    rowptr::Vector{Ti},
    colval::Vector{Ti},
    nzval::Vector{Tv},
) where {Tv, Ti <: Integer}
    ThreadedSparseMatrixCSR(SparseMatrixCSR{1}(m, n, rowptr, colval, nzval))
end

function ThreadedSparseMatrixCSR(a::Transpose{Tv, <:SparseMatrixCSC} where {Tv})
    ThreadedSparseMatrixCSR(SparseMatrixCSR(a))
end

function LinearAlgebra.mul!(
    y::AbstractVector{<:Number},
    A_::ThreadedSparseMatrixCSR,
    x::AbstractVector{<:Number},
    alpha::Number,
    beta::Number,
)
    A = A_.A
    A.n == size(x, 1) || throw(DimensionMismatch())
    A.m == size(y, 1) || throw(DimensionMismatch())

    @batch minbatch = size(y, 1) ÷ Threads.nthreads() for row = 1:size(y, 1)
        @inbounds begin
            v = zero(eltype(y))
            for nz in nzrange(A, row)
                col = A.colval[nz]
                v += A.nzval[nz]*x[col]
            end
            y[row] = alpha*v + beta*y[row]
        end
    end

    return y
end

function LinearAlgebra.mul!(
    y::AbstractVector{<:Number},
    A_::ThreadedSparseMatrixCSR,
    x::AbstractVector{<:Number},
)
    A = A_.A
    A.n == size(x, 1) || throw(DimensionMismatch())
    A.m == size(y, 1) || throw(DimensionMismatch())

    @batch minbatch = max(1, size(y, 1) ÷ Threads.nthreads()) for row = 1:size(y, 1)
        @inbounds begin
            v = zero(eltype(y))
            for nz in nzrange(A, row)
                col = A.colval[nz]
                v += A.nzval[nz]*x[col]
            end
            y[row] = v
        end
    end

    return y
end

function mul(A::ThreadedSparseMatrixCSR, x::AbstractVector)
    y = similar(x, promote_type(eltype(A), eltype(x)), size(A, 1))
    return mul!(y, A, x)
end
*(A::ThreadedSparseMatrixCSR, v::VT) where {VT <: AbstractVector} = mul(A, v)

Base.eltype(A::ThreadedSparseMatrixCSR)            = Base.eltype(A.A)
Base.size(A::ThreadedSparseMatrixCSR)              = Base.size(A.A)
Base.size(A::ThreadedSparseMatrixCSR, i)           = Base.size(A.A, i)
Base.IndexStyle(::Type{<:ThreadedSparseMatrixCSR}) = IndexCartesian()

SparseMatricesCSR.getrowptr(A::ThreadedSparseMatrixCSR) = SparseMatricesCSR.getrowptr(A.A)
SparseMatricesCSR.getnzval(A::ThreadedSparseMatrixCSR)  = SparseMatricesCSR.getnzval(A.A)
SparseMatricesCSR.getcolval(A::ThreadedSparseMatrixCSR) = SparseMatricesCSR.getcolval(A.A)

SparseArrays.issparse(A::ThreadedSparseMatrixCSR) = issparse(A.A)
SparseArrays.nnz(A::ThreadedSparseMatrixCSR)      = nnz(A.A)
SparseArrays.nonzeros(A::ThreadedSparseMatrixCSR) = nonzeros(A.A)

Base.@propagate_inbounds function SparseArrays.getindex(
    A::ThreadedSparseMatrixCSR{T},
    i0::Integer,
    i1::Integer,
) where {T}
    getindex(A.A, i0, i1)
end
SparseArrays.getindex(A::ThreadedSparseMatrixCSR, ::Colon, ::Colon) = copy(A)
SparseArrays.getindex(A::ThreadedSparseMatrixCSR, i::Int, ::Colon) = getindex(A.A, i, 1:size(A, 2))
SparseArrays.getindex(A::ThreadedSparseMatrixCSR, ::Colon, i::Int) = getindex(A.A, 1:size(A, 1), i)

Ferrite.apply_zero!(A::ThreadedSparseMatrixCSR, f::AbstractVector, ch::ConstraintHandler) =
    apply_zero!(A.A, f, ch)
function Ferrite.apply_zero!(K::SparseMatrixCSR, f::AbstractVector, ch::ConstraintHandler)
    # m = Ferrite.meandiag(K)

    Ferrite.zero_out_columns!(K, ch.dofmapping)
    Ferrite.zero_out_rows!(K, ch.prescribed_dofs)

    @inbounds for i = 1:length(ch.inhomogeneities)
        d = ch.prescribed_dofs[i]
        K[d, d] = #m
            if length(f) != 0
                f[d] = 0.0
            end
    end
end

function Ferrite.start_assemble(K::ThreadedSparseMatrixCSR, args...; kwargs...)
    start_assemble(K.A, args...; kwargs...)
end

# struct RHSDataCSR{T}
#     m::T
#     constrained_rows::SparseMatrixCSR{T, Int}
# end

# function Ferrite.get_rhs_data(ch::ConstraintHandler, A::ThreadedSparseMatrixCSR)
#     Ferrite.get_rhs_data(ch, A.A)
# end

# function Ferrite.get_rhs_data(ch::ConstraintHandler, A::SparseMatrixCSR)
#     m = Ferrite.meandiag(A)
#     constrained_rows = A[ch.prescribed_dofs, :]
#     return RHSDataCSR(m, constrained_rows)
# end

# function apply_rhs!(data::RHSDataCSR, f::AbstractVector{T}, ch::ConstraintHandler, applyzero::Bool=false) where T
#     K = data.constrained_rows
#     @assert length(f) == size(K, 1)
#     @boundscheck checkbounds(f, ch.prescribed_dofs)
#     m = data.m

#     # TODO: Can the loops be combined or does the order matter?
#     @inbounds for i in 1:length(ch.inhomogeneities)
#         v = ch.inhomogeneities[i]
#         if !applyzero && v != 0
#             # for j in nzrange(K, i)
#             #     f[K.rowval[j]] -= v * K.nzval[j]
#             # end
#             error("Imhomogeneous bcs not implemented for CSR.")
#         end
#     end
#     @inbounds for (i, pdof) in pairs(ch.prescribed_dofs)
#         dofcoef = ch.dofcoefficients[i]
#         b = ch.inhomogeneities[i]
#         if dofcoef !== nothing # if affine constraint
#             # for (d, v) in dofcoef
#             #     f[d] += f[pdof] * v
#             # end
#             error("Affine bcs not implemented for CSR.")
#         end
#         bz = applyzero ? zero(T) : b
#         f[pdof] = bz * m
#     end
# end

# Internal helper to throw uniform error messages on problems with multiple subdomains
@noinline check_subdomains(dh::Ferrite.AbstractDofHandler) =
    length(dh.subdofhandlers) == 1 ||
    throw(ArgumentError("Using DofHandler with multiple subdomains is not currently supported"))
@noinline check_subdomains(grid::Ferrite.AbstractGrid) =
    length(elementtypes(grid)) == 1 ||
    throw(ArgumentError("Using mixed grid is not currently supported"))

@inline function default_quadrature_order(f, fieldname)
    @unpack dh = f
    @assert fieldname ∈ dh.field_names "Field $fieldname not found in dof handler. Available fields are: $(dh.field_names)."

    for sdh in dh.subdofhandlers
        idx = findfirst(s->s==fieldname, sdh.field_names)
        idx === nothing && continue
        ip = sdh.field_interpolations[idx]
        return max(2*Ferrite.getorder(ip)-1, 2)
    end
end


mtk_parameter_query_filter(discard_me, sym) = false
# The `ModelingToolkit.BasicSymbolic` method lives in `ThunderboltMTKExt`.

"""
    mtk_models()

The `MTKModels` submodule, which holds the prebuilt ModelingToolkit circuit definitions (e.g.
`RSAFDQ2022CircuitMTK`). It lives in `ThunderboltMTKModelsExt` rather than in `Thunderbolt` itself, so
that `ModelingToolkit` and `SciCompDSL` stay weak dependencies; this accessor is how you reach it:

```julia
using ModelingToolkit, SciCompDSL          # loads ThunderboltMTKModelsExt
@mtkcompile sys = Thunderbolt.mtk_models().RSAFDQ2022CircuitMTK()
```

Both packages are needed because `@mtkmodel` comes from `SciCompDSL`. The 3D-0D coupling itself needs
only `ModelingToolkit`.

Errors with an actionable message when the extension is not loaded.
"""
function mtk_models()
    ext = Base.get_extension(@__MODULE__, :ThunderboltMTKModelsExt)
    ext === nothing && error(
        "The ModelingToolkit circuit models live in `ThunderboltMTKModelsExt`, which is not loaded. " *
        "Run `using ModelingToolkit, SciCompDSL` first (`@mtkmodel` comes from SciCompDSL).",
    )
    return ext.MTKModels
end

function query_mtk_parameter_by_symbol(sys, sym::Symbol)
    symbol_list = SymbolicIndexingInterface.parameter_symbols(sys)
    idx = findfirst(param->mtk_parameter_query_filter(param, sym), symbol_list)
    idx === nothing && @error "Symbol $sym not found for system $sys."
    return symbol_list[idx]
end

"""
Examples:

* `DenseDataRange{Vector{Int}, Vector{Int}}` to map dofs (outer index) to elements (inner index)
* `DenseDataRange{Vector{Vec{3,Float64}}, Vector{Int}}` to store fluxes per quadrature point (inner index) per element (outer index)
"""
struct DenseDataRange{DataVectorType, IndexVectorType}
    data::DataVectorType
    offsets::IndexVectorType
end

Base.size(v::DenseDataRange) = size(v.data)
Base.getindex(v::DenseDataRange, i::Int) = getindex(v.data, i)

Base.eltype(data::DenseDataRange) = eltype(data.data)

"""
    get_data_for_index(r::DenseDataRange, i::Integer) -> SubArray

A view on the block of `r` belonging to outer index `i`, delimited by `r.offsets[i]` and
`r.offsets[i+1]`.
"""
@inline function get_data_for_index(r::DenseDataRange, i::Integer)
    i1 = r.offsets[i]
    i2 = r.offsets[i+1]-1
    return @view r.data[i1:i2]
end

# To handle embedded elements in the same code
_inner_product_helper(a::Vec, B::AbstractTensor, c::Vec) = a ⋅ B ⋅ c
_inner_product_helper(a::Vec, B::AbstractFloat, c::Vec) = a ⋅ c * B

function geometric_subdomain_interpolation(sdh::SubDofHandler)
    grid      = get_grid(sdh.dh)
    sdim      = getspatialdim(grid)
    firstcell = getcells(grid, first(sdh.cellset))
    ip_geo    = Ferrite.geometric_interpolation(typeof(firstcell))^sdim
    return ip_geo
end

function get_first_cell(sdh::SubDofHandler)
    grid = get_grid(sdh.dh)
    return getcells(grid, first(sdh.cellset))
end

function adapt_vector_type(::Type{<:Vector}, v::VT) where {VT}
    return v
end

function get_closest_vertex(val::Vec, grid::AbstractGrid)
    distance = Inf
    closest_vertex = VertexIndex(1, 1)
    snap_size = 1e-8
    for (cell_idx, cell) in enumerate(getcells(grid))
        for (vertex_idx, vertex) in enumerate(vertices(cell))
            distance2 = norm(val - get_node_coordinate(grid, vertex))
            if distance2 < distance
                distance = distance2
                closest_vertex = VertexIndex(cell_idx, vertex_idx)
                if distance ≤ snap_size
                    return closest_vertex
                end
            end
        end
    end
    return closest_vertex
end

@generated function dot_2_1(S1::FourthOrderTensor{dim}, S2::SecondOrderTensor{dim}) where {dim}
    idxS1(i, j, k, l) = Tensors.compute_index(Tensors.get_base(S1), i, j, k, l)
    idxS2(i, j) = Tensors.compute_index(Tensors.get_base(S2), i, j)
    exps = Expr(:tuple)
    for l = 1:dim, k = 1:dim, j = 1:dim, i = 1:dim
        ex1 = Expr[:(Tensors.get_data(S1)[$(idxS1(i, m, k, l))]) for m = 1:dim]
        ex2 = Expr[:(Tensors.get_data(S2)[$(idxS2(m, j))]) for m = 1:dim]
        push!(exps.args, Tensors.reducer(ex1, ex2))
    end
    quote
        $(Expr(:meta, :inline))
        @inbounds return Tensor{4, dim}($exps)
    end
end

@generated function dot_2_1t(S1::FourthOrderTensor{dim}, S2::SecondOrderTensor{dim}) where {dim}
    idxS1(i, j, k, l) = Tensors.compute_index(Tensors.get_base(S1), i, j, k, l)
    idxS2(i, j) = Tensors.compute_index(Tensors.get_base(S2), i, j)
    exps = Expr(:tuple)
    for l = 1:dim, k = 1:dim, j = 1:dim, i = 1:dim
        ex1 = Expr[:(Tensors.get_data(S1)[$(idxS1(i, m, k, l))]) for m = 1:dim]
        ex2 = Expr[:(Tensors.get_data(S2)[$(idxS2(j, m))]) for m = 1:dim]
        push!(exps.args, Tensors.reducer(ex1, ex2))
    end
    quote
        $(Expr(:meta, :inline))
        @inbounds return Tensor{4, dim}($exps)
    end
end

@generated function dot_3_1(S1::FourthOrderTensor{dim}, S2::SecondOrderTensor{dim}) where {dim}
    idxS1(i, j, k, l) = Tensors.compute_index(Tensors.get_base(S1), i, j, k, l)
    idxS2(i, j) = Tensors.compute_index(Tensors.get_base(S2), i, j)
    exps = Expr(:tuple)
    for l = 1:dim, k = 1:dim, j = 1:dim, i = 1:dim
        ex1 = Expr[:(Tensors.get_data(S1)[$(idxS1(i, j, m, l))]) for m = 1:dim]
        ex2 = Expr[:(Tensors.get_data(S2)[$(idxS2(m, j))]) for m = 1:dim]
        push!(exps.args, Tensors.reducer(ex1, ex2))
    end
    quote
        $(Expr(:meta, :inline))
        @inbounds return Tensor{4, dim}($exps)
    end
end

@generated function dot_3_1t(S1::FourthOrderTensor{dim}, S2::SecondOrderTensor{dim}) where {dim}
    idxS1(i, j, k, l) = Tensors.compute_index(Tensors.get_base(S1), i, j, k, l)
    idxS2(i, j) = Tensors.compute_index(Tensors.get_base(S2), i, j)
    exps = Expr(:tuple)
    for l = 1:dim, k = 1:dim, j = 1:dim, i = 1:dim
        ex1 = Expr[:(Tensors.get_data(S1)[$(idxS1(i, j, m, l))]) for m = 1:dim]
        ex2 = Expr[:(Tensors.get_data(S2)[$(idxS2(j, m))]) for m = 1:dim]
        push!(exps.args, Tensors.reducer(ex1, ex2))
    end
    quote
        $(Expr(:meta, :inline))
        @inbounds return Tensor{4, dim}($exps)
    end
end

function is_sdh_on_subdomain(sdh, name::String)
    if name == "" # Default is everywhere
        return true
    end
    grid = get_grid(sdh.dh)
    cellset = getcellset(grid, name)
    return first(sdh.cellset) ∈ cellset
end

function is_sdh_on_any_subdomain(sdh, names::Vector{String})
    for name in names
        is_sdh_on_subdomain(sdh, name) && return true
    end
    return false
end

function narrow_dict_types(d::Dict)
    # 1. Gather all unique types of the actual values in the dictionary
    val_types = Tuple(unique(typeof(v) for v in values(d)))

    # 2. Create a Union of those specific types
    NarrowUnion = Union{val_types...}

    # 3. Construct a new dictionary with the exact Key type and the new Union type
    return Dict{keytype(d), NarrowUnion}(d)
end

function collect_dofs_on_subdomain(dh, mesh, name)
    dofs = Set{Int}()
    for sdh in dh.subdofhandlers
        if any([
            CellIndex(first(sdh.cellset)) ∈ subset for
            subset in values(mesh.volumetric_subdomains[name].data)
        ])
            for cellid in sdh.cellset
                for dof in celldofs(sdh, cellid)
                    push!(dofs, dof)
                end
            end
        end
    end
    return sort(collect(dofs))
end

@doc raw"""
    smooth_abs(x, ε)

Smooth approximation of ``|x|``:

```math
    \operatorname{smooth\_abs}(x, \varepsilon) = \frac{x^2}{\sqrt{x^2 + \varepsilon^2}} .
```

Needed wherever ``|x|`` enters a residual that is differentiated to obtain a tangent. ``|x|`` has a
kink at the origin, so its linearization is discontinuous there and Newton's method degrades to
semismooth behaviour exactly at ``x = 0`` — which for a rate ``x`` is the physically common case of
"currently not moving", not an exotic corner.

Chosen over the more familiar pseudo-Huber ``\sqrt{x^2 + \varepsilon^2} - \varepsilon`` for two
reasons:

- **Asymptotically exact.** Both vanish at the origin, but this form additionally has error
  ``\mathcal{O}(\varepsilon^2 / |x|)`` for ``|x| \gg \varepsilon``, where the pseudo-Huber keeps an
  ``\mathcal{O}(\varepsilon)`` bias for arbitrarily large arguments.
- **No cancellation.** ``\sqrt{x^2 + \varepsilon^2} - \varepsilon`` subtracts two nearly equal
  numbers when ``|x| \ll \varepsilon``.

Both the value and the derivative vanish at the origin; a vanishing derivative is the only value a
smooth, even approximation can take there.

Requires ``\varepsilon > 0``.
"""
smooth_abs(x, ε) = x^2 / sqrt(x^2 + ε^2)
