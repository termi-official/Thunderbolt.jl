# ----------------------------------- SciMLBase.jl Integrator Interface ------------------------------------
SciMLBase.has_stats(::ThunderboltTimeIntegrator) = true

SciMLBase.has_tstop(integrator::ThunderboltTimeIntegrator) = !isempty(integrator.opts.tstops)
SciMLBase.first_tstop(integrator::ThunderboltTimeIntegrator) = first(integrator.opts.tstops)
SciMLBase.pop_tstop!(integrator::ThunderboltTimeIntegrator) = pop!(integrator.opts.tstops)

function SciMLBase.add_tstop!(integrator::ThunderboltTimeIntegrator, t)
    integrator.tdir * (t - integrator.t) < zero(integrator.t) &&
        error("Tried to add a tstop that is behind the current time. This is strictly forbidden")
    push!(integrator.opts.tstops, integrator.tdir * t)
end

@inline function SciMLBase.get_tmp_cache(integrator::ThunderboltTimeIntegrator)
    return (integrator.cache.tmp,)
end
@inline function SciMLBase.get_tmp_cache(
    integrator::ThunderboltTimeIntegrator,
    alg::AbstractSolver,
    cache::AbstractTimeSolverCache,
)
    return (cache.tmp,)
end

function SciMLBase.terminate!(
    integrator::ThunderboltTimeIntegrator,
    retcode = ReturnCode.Terminated,
)
    integrator.sol = SciMLBase.solution_new_retcode(integrator.sol, retcode)
    integrator.opts.tstops.valtree = typeof(integrator.opts.tstops.valtree)()
end

# @inline function SciMLBase.get_du(integrator::ThunderboltTimeIntegrator)
# end

@inline SciMLBase.get_proposed_dt(integrator::ThunderboltTimeIntegrator) = integrator.dt

@inline function SciMLBase.derivative_discontinuity!(
    integrator::ThunderboltTimeIntegrator,
    bool::Bool,
)
    integrator.derivative_discontinuity = bool
end

SciMLBase.get_sol(integrator::ThunderboltTimeIntegrator) = integrator.sol

function SciMLBase.set_proposed_dt!(integrator::ThunderboltTimeIntegrator, dt)
    if integrator.dtchangeable == true
        integrator.dt = dt
        # `modify_dt_for_tstops!` restores `dt` from `dtcache` at every header, so writing
        # only `dt` loses the proposal after one step. Upstream writes both
        # (OrdinaryDiffEqCore `integrator_interface.jl:180-182`).
        integrator.dtpropose = dt
        integrator.dtcache = dt
    elseif integrator.dt != dt
        error("Trying to change dt on constant time step integrator.")
    end
end

function SciMLBase.isadaptive(integrator::ThunderboltTimeIntegrator)
    # A dummy controller is the *absence* of control, so it answers like no controller at all. Without
    # this an algorithm that merely *permits* a controller would report an adaptive integrator on its
    # default configuration, which would change how a failed step is handled (`post_newton_controller!`
    # shrinking `dt` instead of reporting `ConvergenceFailure`) for solves that never asked to adapt.
    # `should_accept_step` and `adapt_dt!` already treat the two cases identically.
    integrator.controller_cache isa Union{Nothing, DummyControllerCache} && return false
    if !SciMLBase.isadaptive(integrator.alg)
        error(
            "Algorithm $(integrator.alg) is not adaptive, but the integrator is trying to adapt. Aborting.",
        )
    end
    return true
end

function SciMLBase.last_step_failed(integrator::ThunderboltTimeIntegrator)
    integrator.last_step_failed
end

SciMLBase.postamble!(integrator::ThunderboltTimeIntegrator) = _postamble!(integrator)

function SciMLBase.savevalues!(
    integrator::ThunderboltTimeIntegrator,
    force_save = false,
    reduce_size = true,
)
    OrdinaryDiffEqCore._savevalues!(integrator, force_save, reduce_size)
end

# ---------------------------------- DiffEqBase.jl Interface ------------------------------------
DiffEqBase.get_tstops(integ::ThunderboltTimeIntegrator) = integ.opts.tstops
DiffEqBase.get_tstops_array(integ::ThunderboltTimeIntegrator) = get_tstops(integ).valtree
DiffEqBase.get_tstops_max(integ::ThunderboltTimeIntegrator) = maximum(get_tstops_array(integ))


SciMLBase.has_reinit(integrator::ThunderboltTimeIntegrator) = true
function DiffEqBase.reinit!(
    integrator::ThunderboltTimeIntegrator,
    u0 = integrator.sol.prob.u0;
    t0 = integrator.sol.prob.tspan[1],
    tf = integrator.sol.prob.tspan[2],
    dt0 = tf-t0,
    erase_sol = false,
    tstops = integrator.opts.tstops_cache,
    saveat = integrator.opts.saveat_cache,
    d_discontinuities = integrator.opts.d_discontinuities_cache,
    reinit_callbacks = true,
    reinit_retcode = true,
    reinit_cache = true,
)
    recursivecopy!(integrator.u, u0)
    recursivecopy!(integrator.uprev, integrator.u)
    integrator.t = t0
    integrator.tprev = t0

    integrator.iter = 0
    integrator.derivative_discontinuity = false

    # A reinit'd integrator has not failed and is not mid-tstop.
    integrator.force_stepfail = false
    integrator.last_step_failed = false
    integrator.isout = false
    integrator.just_hit_tstop = false
    integrator.next_step_tstop = false
    integrator.tstop_target = integrator.t

    integrator.stats.naccept = 0
    integrator.stats.nreject = 0

    # `saveiter`/`saveiter_dense` index into the solution buffers, so they follow whatever
    # happens to those. Operator splitting children carry a `DummyODESolution`, which has
    # no buffers at all.
    if hasproperty(integrator.sol, :t)
        if erase_sol
            resize!(integrator.sol.t, 0)
            resize!(integrator.sol.u, 0)
            integrator.saveiter = 0
            integrator.saveiter_dense = 0
        else
            integrator.saveiter = min(integrator.saveiter, length(integrator.sol.t))
            integrator.saveiter_dense = min(integrator.saveiter_dense, integrator.saveiter)
        end
    end

    if reinit_callbacks
        DiffEqBase.initialize!(integrator.opts.callback, u0, t0, integrator)
    elseif !isempty(integrator.opts.callback.discrete_callbacks)
        # always reinit the saving callback so that t0 can be saved if needed
        saving_callback = integrator.opts.callback.discrete_callbacks[end]
        DiffEqBase.initialize!(saving_callback, u0, t0, integrator)
    end
    if reinit_retcode
        integrator.sol =
            SciMLBase.solution_new_retcode(integrator.sol, SciMLBase.ReturnCode.Default)
    end

    tType = typeof(integrator.t)
    tspan = (tType(t0), tType(tf))
    integrator.opts.tstops =
        OrdinaryDiffEqCore.initialize_tstops(tType, tstops, d_discontinuities, tspan)
    integrator.opts.saveat = OrdinaryDiffEqCore.initialize_saveat(tType, saveat, tspan)
    integrator.opts.d_discontinuities =
        OrdinaryDiffEqCore.initialize_d_discontinuities(tType, d_discontinuities, tspan)

    if reinit_cache
        DiffEqBase.initialize!(integrator, integrator.cache)
    end
end


# ----------------------------------- OrdinaryDiffEqCore compat ----------------------------------
OrdinaryDiffEqCore.has_discontinuity(integrator::ThunderboltTimeIntegrator) =
    !isempty(integrator.opts.d_discontinuities)
OrdinaryDiffEqCore.first_discontinuity(integrator::ThunderboltTimeIntegrator) =
    first(integrator.opts.d_discontinuities)
OrdinaryDiffEqCore.pop_discontinuity!(integrator::ThunderboltTimeIntegrator) =
    pop!(integrator.opts.d_discontinuities)

function _postamble!(integrator)
    DiffEqBase.finalize!(integrator.opts.callback, integrator.u, integrator.t, integrator)
    OrdinaryDiffEqCore.solution_endpoint_match_cur_integrator!(integrator)
    fix_solution_buffer_sizes!(integrator, integrator.sol)
    finalize_integration_monitor(integrator)
end

OrdinaryDiffEqCore.alg_extrapolates(alg::AbstractSolver) = false

OrdinaryDiffEqCore.choose_algorithm!(integrator, cache::AbstractTimeSolverCache) = nothing

function OrdinaryDiffEqCore.perform_step!(
    integ::ThunderboltTimeIntegrator,
    cache::AbstractTimeSolverCache,
)
    if !perform_step!(integ.f, cache, integ.t, integ.dt)
        integ.force_stepfail = true
    end
    return nothing
end

# --------------------------- New Interface Stuff (to be upstreamed) ---------------------------------

# Solution looping interface
function should_accept_step(integrator::ThunderboltTimeIntegrator)
    if integrator.force_stepfail || integrator.isout
        return false
    end
    return should_accept_step(integrator, integrator.cache, integrator.controller_cache)
end
function should_accept_step(
    integrator::ThunderboltTimeIntegrator,
    cache,
    ::Union{Nothing, DummyControllerCache},
)
    return !(integrator.force_stepfail)
end

# `stats.naccept` is counted in `step_footer!`, which sees every accepted attempt
# including the last one; this header-side hook only prepares the next step.
function accept_step!(integrator::ThunderboltTimeIntegrator)
    accept_step!(integrator, integrator.cache, integrator.controller_cache)
end
function accept_step!(integrator::ThunderboltTimeIntegrator, cache, controller)
    store_previous_info!(integrator)
end

function store_previous_info!(integrator::ThunderboltTimeIntegrator)
    if length(integrator.uprev) > 0 # Integrator can rollback
        update_uprev!(integrator)
    end
end

function step_header!(integrator::ThunderboltTimeIntegrator)
    # Accept or reject the step
    if !is_first_iteration(integrator)
        if should_accept_step(integrator)
            accept_step!(integrator)
        else # Step should be rejected and hence repeated
            reject_step!(integrator)
        end
    elseif integrator.derivative_discontinuity # && integrator.iter == 0
        update_uprev!(integrator)
    end

    # Before stepping we might need to adjust the dt
    increment_iteration(integrator)
    OrdinaryDiffEqCore.choose_algorithm!(integrator, integrator.cache)
    OrdinaryDiffEqCore.fix_dt_at_bounds!(integrator)
    OrdinaryDiffEqCore.modify_dt_for_tstops!(integrator)
    integrator.force_stepfail = false

    # Log here so that t, dt, and iter all describe the step about to be taken.
    integration_monitor_step(integrator)
end

function update_uprev!(integrator::ThunderboltTimeIntegrator)
    # # OrdinaryDiffEqCore.update_uprev!(integrator) # FIXME recover
    # if alg_extrapolates(integrator.alg)
    #     if isinplace(integrator.sol.prob)
    #         recursivecopy!(integrator.uprev2, integrator.uprev)
    #     else
    #         integrator.uprev2 = integrator.uprev
    #     end
    # end
    # if isinplace(integrator.sol.prob) # This should be dispatched in the integrator directly
    recursivecopy!(integrator.uprev, integrator.u)
    if integrator.alg isa OrdinaryDiffEqCore.DAEAlgorithm
        recursivecopy!(integrator.duprev, integrator.du)
    end
    # else
    #     integrator.uprev = integrator.u
    #     if integrator.alg isa DAEAlgorithm
    #         integrator.duprev = integrator.du
    #     end
    # end
    nothing
end

function controller_message_on_dtmin_error(integrator::SciMLBase.DEIntegrator)
    if isdefined(integrator, :EEst)
        return ", and step error estimate = $(integrator.EEst)"
    else
        return ""
    end
end

function SciMLBase.check_error(integrator::ThunderboltTimeIntegrator)
    if integrator.sol.retcode ∉ (SciMLBase.ReturnCode.Success, SciMLBase.ReturnCode.Default)
        return integrator.sol.retcode
    end
    opts = integrator.opts
    verbose = opts.verbose
    # This implementation is intended to be used for ODEIntegrator and
    # SDEIntegrator.
    if isnan(integrator.dt)
        @SciMLMessage(
            "NaN dt detected. Likely a NaN value in the state, parameters, or derivative value caused this outcome.",
            verbose,
            :dt_NaN
        )
        return SciMLBase.ReturnCode.DtNaN
    end
    if hasproperty(integrator, :iter) &&
       hasproperty(opts, :maxiters) &&
       integrator.iter > opts.maxiters
        @SciMLMessage(
            "Interrupted. Larger maxiters is needed. If you are using an integrator for non-stiff ODEs or an automatic switching algorithm (the default), you may want to consider using a method for stiff equations. See the solver pages for more details (e.g. https://docs.sciml.ai/DiffEqDocs/stable/solvers/ode_solve/#Stiff-Problems).",
            verbose,
            :max_iters
        )
        return SciMLBase.ReturnCode.MaxIters
    end

    # The last part:
    # Bail out if we take a step with dt less than the minimum value (which may be time dependent)
    # except if we are successfully taking such a small timestep is to hit a tstop exactly
    # We also exit if the ODE is unstable according to a user chosen callback
    # but only if we accepted the step to prevent from bailing out as unstable
    # when we just took way too big a step)
    # `step_header!` has already cleared `force_stepfail` for the attempt about to be
    # made, so `should_accept_step` cannot see a rejection here; `last_step_failed`
    # survives the header.
    step_rejected = SciMLBase.last_step_failed(integrator)
    step_accepted = should_accept_step(integrator)
    force_dtmin   = hasproperty(integrator, :force_dtmin) && integrator.force_dtmin
    if !force_dtmin && SciMLBase.isadaptive(integrator)
        dt_below_min      = abs(integrator.dt) ≤ abs(opts.dtmin)
        before_next_tstop = SciMLBase.has_tstop(integrator) ? integrator.t + integrator.dt < integrator.tdir * SciMLBase.first_tstop(integrator) : true
        if dt_below_min && (step_rejected || before_next_tstop)
            @SciMLMessage(
                lazy"dt($(integrator.dt)) <= dtmin($(opts.dtmin)) at t=$(integrator.t)$(controller_message_on_dtmin_error(integrator)). Aborting. There is either an error in your model specification or the true solution is unstable.",
                verbose,
                :dt_min_unstable
            )
            return SciMLBase.ReturnCode.DtLessThanMin
        elseif step_rejected &&
               integrator.t isa AbstractFloat &&
               abs(integrator.dt) <= abs(eps(integrator.t)) # = DiffEqBase.timedepentdtmin(integrator)
            @SciMLMessage(
                lazy"At t=$(integrator.t), dt was forced below floating point epsilon $(integrator.dt)$(controller_message_on_dtmin_error(integrator)). Aborting. There is either an error in your model specification or the true solution is unstable (or the true solution can not be represented in the precision of $(eltype(integrator.u))).",
                verbose,
                :dt_epsilon
            )
            return SciMLBase.ReturnCode.Unstable
        end
    end
    if step_accepted && (
        hasproperty(opts, :unstable_check) &&
        opts.unstable_check(integrator.dt, integrator.u, integrator.p, integrator.t)
    )
        @SciMLMessage("Instability detected. Aborting", verbose, :instability)
        return SciMLBase.ReturnCode.Unstable
    end
    if SciMLBase.last_step_failed(integrator) && !SciMLBase.isadaptive(integrator)
        @SciMLMessage(
            "Newton steps could not converge and algorithm is not adaptive. Use a lower dt.",
            verbose,
            :newton_convergence
        )
        return SciMLBase.ReturnCode.ConvergenceFailure
    end
    return SciMLBase.ReturnCode.Success
end

function footer_reset_flags!(integrator)
    integrator.derivative_discontinuity = false
end

function fix_solution_buffer_sizes!(integrator, sol)
    resize!(integrator.sol.t, integrator.saveiter)
    resize!(integrator.sol.u, integrator.saveiter)
    if !(integrator.sol isa SciMLBase.DAESolution)
        resize!(integrator.sol.k, integrator.saveiter_dense)
    end
end

function setup_validity_flags!(integrator, t_next)
    integrator.isout = integrator.opts.isoutofdomain(integrator.u, integrator.p, t_next)
end

function step_footer!(integrator::ThunderboltTimeIntegrator)
    t_start = integrator.t
    dt_step = integrator.dt
    ttmp = t_start + integrator.tdir * dt_step

    footer_reset_flags!(integrator)
    setup_validity_flags!(integrator, ttmp)

    accepted = should_accept_step(integrator)
    if accepted
        OrdinaryDiffEqCore.increment_accept!(integrator.stats)
        integrator.last_step_failed = false
        integrator.tprev = t_start
        integrator.t = OrdinaryDiffEqCore.fixed_t_for_tstop_error!(integrator, ttmp)
        OrdinaryDiffEqCore.handle_callbacks!(integrator)
        adapt_dt!(integrator) # Noop for non-adaptive algorithms
    elseif integrator.force_stepfail
        if SciMLBase.isadaptive(integrator)
            OrdinaryDiffEqCore.post_newton_controller!(integrator, integrator.alg)
            # elseif integrator.dtchangeable # Non-adaptive but can change dt
            #     integrator.dt *= integrator.opts.failfactor
        elseif integrator.last_step_failed
            integration_monitor_step_footer(integrator, t_start, ttmp, accepted, dt_step)
            return
        end
        integrator.last_step_failed = true
    end

    t_end = accepted ? integrator.t : ttmp
    integration_monitor_step_footer(integrator, t_start, t_end, accepted, dt_step)

    return nothing
end

# `modify_dt_for_tstops!` decides, before the step, whether it ends exactly on a tstop;
# `fixed_t_for_tstop_error!` then sets `t` to the recorded target rather than to the
# accumulated `t + dt`. The upstream fallbacks are `nothing`/`false`, so without these
# three methods the mechanism only half runs.
@inline function OrdinaryDiffEqCore._set_tstop_flag!(
    integrator::ThunderboltTimeIntegrator,
    is_tstop::Bool,
    target = nothing,
)
    integrator.next_step_tstop = is_tstop
    if is_tstop && target !== nothing
        integrator.tstop_target = target
    end
    return nothing
end
@inline OrdinaryDiffEqCore._get_next_step_tstop(integrator::ThunderboltTimeIntegrator) =
    integrator.next_step_tstop
@inline OrdinaryDiffEqCore._get_tstop_target(integrator::ThunderboltTimeIntegrator) =
    integrator.tstop_target

is_first_iteration(integrator) = integrator.iter == 0
increment_iteration(integrator) = integrator.iter += 1

function integration_monitor_step(integrator)
    if integrator.opts.progress && integrator.iter % integrator.opts.progress_steps == 0
        integration_step_monitor(integrator, integrator.opts.progress_monitor)
    end
end

function integration_monitor_step_footer(integrator, t_start, t_end, accepted, dt_step)
    if integrator.opts.progress && integrator.iter % integrator.opts.progress_steps == 0
        integration_step_footer_monitor(
            integrator,
            t_start,
            t_end,
            accepted,
            dt_step,
            integrator.opts.progress_monitor,
        )
    end
end

function finalize_integration_monitor(integrator)
    if integrator.opts.progress
        integration_finalize_monitor(integrator, integrator.opts.progress_monitor)
    end
end

notify_integrator_hit_tstop!(integrator::ThunderboltTimeIntegrator) =
    integrator.just_hit_tstop = true

# TODO upstream into OrdinaryDiffEqCore
function compute_rate_prototype(prob)
    u = prob.u0

    tType = eltype(prob.tspan)
    tTypeNoUnits = typeof(one(tType))

    isdae = (
        prob.f.mass_matrix != I &&
        !(prob.f.mass_matrix isa Tuple) &&
        ArrayInterface.issingular(prob.f.mass_matrix)
    )
    if !isdae &&
       isinplace(prob) &&
       u isa AbstractArray &&
       eltype(u) <: Number &&
       uBottomEltypeNoUnits == uBottomEltype &&
       tType == tTypeNoUnits # Could this be more efficient for other arrays?
        return recursivecopy(u)
    else
        _compute_rate_prototype_mass_matrix_form(prob)
    end
end
function compute_rate_prototype(prob::SciMLBase.DiscreteProblem)
    _compute_rate_prototype_mass_matrix_form(prob)
end
function _compute_rate_prototype_mass_matrix_form(prob)
    u = prob.u0

    tType                = eltype(prob.tspan)
    tTypeNoUnits         = typeof(one(tType))
    uBottomEltype        = OrdinaryDiffEqCore.recursive_bottom_eltype(u)
    uBottomEltypeNoUnits = OrdinaryDiffEqCore.recursive_unitless_bottom_eltype(u)
    if (uBottomEltypeNoUnits == uBottomEltype && tType == tTypeNoUnits) || eltype(u) <: Enum
        return u
    else # has units!
        return u / oneunit(tType)
    end
end
function compute_rate_prototype(prob::SciMLBase.DAEProblem)
    return prob.du0
end

function compute_rate_prototype(prob::AbstractSemidiscreteProblem)
    _compute_rate_prototype_mass_matrix_form(prob)
end

function OrdinaryDiffEqCore.handle_tstop!(integrator::ThunderboltTimeIntegrator)
    if SciMLBase.has_tstop(integrator)
        tdir_t = integrator.tdir * integrator.t
        tdir_tstop = SciMLBase.first_tstop(integrator)
        if tdir_t == tdir_tstop
            while tdir_t == tdir_tstop #remove all redundant copies
                res = SciMLBase.pop_tstop!(integrator)
                SciMLBase.has_tstop(integrator) ? (tdir_tstop = SciMLBase.first_tstop(integrator)) :
                break
            end
            notify_integrator_hit_tstop!(integrator)
        elseif tdir_t > tdir_tstop
            if !integrator.dtchangeable
                SciMLBase.change_t_via_interpolation!(
                    integrator,
                    integrator.tdir * SciMLBase.pop_tstop!(integrator),
                    Val{true},
                )
                notify_integrator_hit_tstop!(integrator)
            else
                error(
                    "Something went wrong. Integrator stepped past tstops but the algorithm was dtchangeable. Please report this error.",
                )
            end
        end
    end
    return nothing
end

OrdinaryDiffEqCore.isfsal(::AbstractSolver) = false
OrdinaryDiffEqCore._get_W(::ThunderboltTimeIntegrator) = nothing

function SciMLBase.change_t_via_interpolation!(
    integrator::ThunderboltTimeIntegrator,
    t,
    modify_save_endpoint::Type{Val{T}} = Val{false},
    reinitialize_alg = nothing,
) where {T}
    OrdinaryDiffEqCore._change_t_via_interpolation!(
        integrator,
        t,
        modify_save_endpoint,
        reinitialize_alg,
    )
    return nothing
end

function OrdinaryDiffEqCore.post_newton_controller!(
    integrator::ThunderboltTimeIntegrator,
    alg::AbstractSolver,
)
    # Same shrink law as OrdinaryDiffEqCore's generic (`controllers.jl:481-484`), but
    # reading the option instead of hardcoding its default.
    integrator.dt = integrator.dt / integrator.opts.failfactor
end
