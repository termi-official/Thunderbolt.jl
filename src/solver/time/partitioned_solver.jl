abstract type AbstractPointwiseSolver <: AbstractSolver end
abstract type AbstractPointwiseSolverCache <: AbstractTimeSolverCache end

SciMLBase.isadaptive(::AbstractPointwiseSolver) = false
OrdinaryDiffEqCore.default_controller(QT, ::AbstractPointwiseSolver) =
    OrdinaryDiffEqCore.DummyController()

# Auxilliary functions to query the coordinate
@inline getcoordinate(f::F, i::I) where {F <: AbstractPointwiseSolverCache, I}                           = getcoordinate(f, i, f.xs)
@inline getcoordinate(f::F, i::I, x::X) where {F <: AbstractPointwiseSolverCache, I, X <: Nothing}       = nothing
@inline getcoordinate(f::F, i::I, x::X) where {F <: AbstractPointwiseSolverCache, I, X <: AbstractArray} = x[i]

# Redirect to inner solve
function perform_step!(
    f::PointwiseODEFunction,
    cache::AbstractPointwiseSolverCache,
    t::Real,
    Δt::Real,
)
    @timeit_debug "reaction solve" _pointwise_step_outer_kernel!(f, t, Δt, cache, cache.uₙ)
end

function perform_step!(
    fs::PointwiseMultiODEFunction,
    cache::AbstractPointwiseSolverCache,
    t::Real,
    Δt::Real,
)
    @timeit_debug "reaction solve" for (i, f) in enumerate(fs.functions)
        ! _pointwise_step_outer_kernel!(f, t, Δt, repack_subdomain(cache, i), cache.uₙ) &&
            return false
    end

    return true
end

# This controls the outer loop over the ODEs
function _pointwise_step_outer_kernel!(
    f::PointwiseODEFunction,
    t::Real,
    Δt::Real,
    cache::AbstractPointwiseSolverCache,
    ::Union{Vector, SubArray{<:Any, 1, <:Vector}},
)
    npoints = length(f.associated_states) ÷ num_states(f.ode)

    @batch minbatch=cache.batch_size_hint for i ∈ 1:npoints
        _pointwise_step_inner_kernel!(f.ode, i, t, Δt, cache) || return false
    end

    return true
end

"""
Simple forward euler to solve the cell model.
"""
Base.@kwdef struct ForwardEulerCellSolver{SolutionVectorType} <: AbstractPointwiseSolver
    solution_vector_type::Type{SolutionVectorType} = Vector{Float64}
    batch_size_hint::Int                           = 32
end

# Fully accelerator compatible
struct ForwardEulerCellSolverCache{duType, uType, uprevType, dumType, umType, xType} <:
       AbstractPointwiseSolverCache
    du::duType
    # These vectors hold the data. uₙ may view an outer solution vector while uₙ₋₁ is
    # either aliased to uₙ or a separate rollback buffer, so the types may differ.
    uₙ::uType
    uₙ₋₁::uprevType
    # These array view the data above to give easy indices of the form [ode index, local state index]
    dumat::dumType
    uₙmat::umType
    # uₙ₋₁mat::umType
    batch_size_hint::Int
    xs::xType
end
Adapt.@adapt_structure ForwardEulerCellSolverCache

# This is the actual solver
@inline function _pointwise_step_inner_kernel!(
    cell_model::F,
    i::I,
    t::T,
    Δt::T,
    cache::C,
) where {F, C <: ForwardEulerCellSolverCache, T <: Real, I <: Integer}
    u_local  = @view cache.uₙmat[i, :]
    du_local = @view cache.dumat[i, :]
    x        = getcoordinate(cache, i)

    # TODO get Cₘ
    cell_rhs!(du_local, u_local, x, t, cell_model)

    @inbounds for j = 1:length(u_local)
        u_local[j] += Δt*du_local[j]
    end

    return true
end

function setup_solver_cache(
    f::PointwiseODEFunction,
    solver::ForwardEulerCellSolver,
    t₀;
    u = nothing,
    uprev = nothing,
)
    (; ode) = f
    npoints = length(f.associated_states) ÷ num_states(f.ode)
    ndofs_local = num_states(ode)

    du = create_system_vector(solver.solution_vector_type, f)
    dumat = reshape(du, (npoints, ndofs_local))
    uₙ = u === nothing ? create_system_vector(solver.solution_vector_type, f) : u
    uₙ₋₁ = uₙ
    uₙmat = reshape(uₙ, (npoints, ndofs_local))
    # `adapt_vector_type`, not `Adapt.adapt`: the coordinates are a vector of `Vec`s or of generalized
    # coordinates, so only the *container* follows `solution_vector_type`, never the element type. The
    # other three cache setups already do this; this one did not, and it only went unnoticed because
    # `f.x` was `nothing` until a coordinate system could be attached to the model.
    xs = f.x === nothing ? nothing : adapt_vector_type(solver.solution_vector_type, f.x)

    return ForwardEulerCellSolverCache(du, uₙ, uₙ₋₁, dumat, uₙmat, solver.batch_size_hint, xs)
end

function setup_solver_cache(
    fs::PointwiseMultiODEFunction,
    solver::ForwardEulerCellSolver,
    t₀;
    u = nothing,
    uprev = nothing,
)
    du   = create_system_vector(solver.solution_vector_type, fs)
    uₙ   = u === nothing ? create_system_vector(solver.solution_vector_type, fs) : u
    uₙ₋₁ = uprev === nothing ? uₙ : uprev

    dumat = [
        reshape(
            view(du, f.associated_states),
            (num_states(f.ode), length(f.associated_states) ÷ num_states(f.ode)),
        )' for f in fs.functions
    ]
    uₙmat = [
        reshape(
            view(uₙ, f.associated_states),
            (num_states(f.ode), length(f.associated_states) ÷ num_states(f.ode)),
        )' for f in fs.functions
    ]
    xs = [
        f.x === nothing ? nothing : adapt_vector_type(solver.solution_vector_type, f.x) for
        f in fs.functions
    ]

    return ForwardEulerCellSolverCache(du, uₙ, uₙ₋₁, dumat, uₙmat, solver.batch_size_hint, xs)
end

function repack_subdomain(cache::ForwardEulerCellSolverCache, i)
    ForwardEulerCellSolverCache(
        cache.du,
        cache.uₙ,
        cache.uₙ₋₁,
        cache.dumat[i],
        cache.uₙmat[i],
        cache.batch_size_hint,
        cache.xs[i],
    )
end

"""
    AdaptiveForwardEulerSubstepper(; substeps, reaction_threshold, solution_vector_type, batch_size_hint)

Explicit pointwise solver which spends its work where the reaction is active: a local system whose
transmembrane potential rate stays below `reaction_threshold` takes a single forward Euler step over
the outer `Δt`, and one that exceeds it takes `substeps` steps of `Δt/substeps` instead.

The decision is made per local system and per step, from the rate at the beginning of the step.
"""
Base.@kwdef struct AdaptiveForwardEulerSubstepper{T, SolutionVectorType <: AbstractVector{T}} <:
                   AbstractPointwiseSolver
    substeps::Int                                  = 10
    reaction_threshold::T                          = 0.1
    solution_vector_type::Type{SolutionVectorType} = Vector{Float64}
    batch_size_hint::Int                           = 32
end

# Fully accelerator compatible
struct AdaptiveForwardEulerSubstepperCache{T, duType, uType, uprevType, dumType, umType, xType} <:
       AbstractPointwiseSolverCache
    du::duType
    # These vectors hold the data. uₙ may view an outer solution vector while uₙ₋₁ is
    # either aliased to uₙ or a separate rollback buffer, so the types may differ.
    uₙ::uType
    uₙ₋₁::uprevType
    # These array view the data above to give easy indices of the form [ode index, local state index]
    dumat::dumType
    uₙmat::umType
    # Solver parameters
    substeps::Int
    reaction_threshold::T
    batch_size_hint::Int
    xs::xType
end
Adapt.@adapt_structure AdaptiveForwardEulerSubstepperCache

@inline function _pointwise_step_inner_kernel!(
    cell_model::F,
    i::I,
    t::T,
    Δt::T,
    cache::C,
) where {F, C <: AdaptiveForwardEulerSubstepperCache, T <: Real, I <: Integer}
    u_local  = @view cache.uₙmat[i, :]
    du_local = @view cache.dumat[i, :]
    x        = getcoordinate(cache, i)

    φₘidx = transmembranepotential_index(cell_model)

    # TODO get Cₘ
    cell_rhs!(du_local, u_local, x, t, cell_model)

    if abs(du_local[φₘidx]) < cache.reaction_threshold
        for j = 1:length(u_local)
            u_local[j] += Δt*du_local[j]
        end
    else
        Δtₛ = Δt/cache.substeps
        for j = 1:length(u_local)
            u_local[j] += Δtₛ*du_local[j]
        end

        for substep ∈ 2:cache.substeps
            tₛ = t + (substep - 1)*Δtₛ
            #TODO Cₘ
            cell_rhs!(du_local, u_local, x, tₛ, cell_model)

            for j = 1:length(u_local)
                u_local[j] += Δtₛ*du_local[j]
            end
        end
    end

    return true
end

function setup_solver_cache(
    f::PointwiseODEFunction,
    solver::AdaptiveForwardEulerSubstepper,
    t₀;
    u = nothing,
    uprev = nothing,
)
    (; ode) = f
    npoints = length(f.associated_states) ÷ num_states(f.ode)
    ndofs_local = num_states(ode)

    du = create_system_vector(solver.solution_vector_type, f)
    dumat = reshape(du, (npoints, ndofs_local))
    uₙ = u === nothing ? create_system_vector(solver.solution_vector_type, f) : u
    uₙ₋₁ = uₙ
    uₙmat = reshape(uₙ, (npoints, ndofs_local))
    xs = if f.x === nothing
        nothing
    else
        adapt_vector_type(solver.solution_vector_type, f.x)
    end

    return AdaptiveForwardEulerSubstepperCache(
        du,
        uₙ,
        uₙ₋₁,
        dumat,
        uₙmat,
        solver.substeps,
        solver.reaction_threshold,
        solver.batch_size_hint,
        xs,
    )
end

function setup_solver_cache(
    fs::PointwiseMultiODEFunction,
    solver::AdaptiveForwardEulerSubstepper,
    t₀;
    u = nothing,
    uprev = nothing,
)
    du   = create_system_vector(solver.solution_vector_type, fs)
    uₙ   = u === nothing ? create_system_vector(solver.solution_vector_type, fs) : u
    uₙ₋₁ = uprev === nothing ? uₙ : uprev

    dumat = [
        reshape(
            view(du, f.associated_states),
            (num_states(f.ode), length(f.associated_states) ÷ num_states(f.ode)),
        )' for f in fs.functions
    ]
    uₙmat = [
        reshape(
            view(uₙ, f.associated_states),
            (num_states(f.ode), length(f.associated_states) ÷ num_states(f.ode)),
        )' for f in fs.functions
    ]
    xs = [
        f.x === nothing ? nothing : adapt_vector_type(solver.solution_vector_type, f.x) for
        f in fs.functions
    ]

    return AdaptiveForwardEulerSubstepperCache(
        du,
        uₙ,
        uₙ₋₁,
        dumat,
        uₙmat,
        solver.substeps,
        solver.reaction_threshold,
        solver.batch_size_hint,
        xs,
    )
end

function repack_subdomain(cache::AdaptiveForwardEulerSubstepperCache, i)
    AdaptiveForwardEulerSubstepperCache(
        cache.du,
        cache.uₙ,
        cache.uₙ₋₁,
        cache.dumat[i],
        cache.uₙmat[i],
        cache.substeps,
        cache.reaction_threshold,
        cache.batch_size_hint,
        cache.xs[i],
    )
end
