abstract type AbstractSolver <: SciMLBase.AbstractDEAlgorithm end
abstract type AbstractNonlinearSolver <: AbstractSolver end

abstract type AbstractNonlinearSolverCache end

abstract type AbstractTimeSolverCache end

# TODO remove
getJ(op) = op.J

# Nonlinear
setup_stage_operator(f::NullFunction, solver::AbstractSolver, local_solver_cache, t₀) =
    NullOperator{Float64, solution_size(f), solution_size(f)}()

# Linear
# Unrolled to disambiguate
function setup_operator(
    strategy::AssemblyStrategy{<:FullAssembly, SequentialScheduling, <:AbstractCPUDevice},
    ::LinearIntegrator{<:NoStimulationProtocol},
    solver::AbstractSolver,
    dh::AbstractDofHandler,
)
    LinearNullOperator{value_type(strategy.device), ndofs(dh)}()
end
function setup_operator(
    strategy::AssemblyStrategy{<:FullAssembly, <:ColoredScheduling, <:AbstractCPUDevice},
    ::LinearIntegrator{<:NoStimulationProtocol},
    solver::AbstractSolver,
    dh::AbstractDofHandler,
)
    LinearNullOperator{value_type(strategy.device), ndofs(dh)}()
end
function setup_operator(
    strategy::AssemblyStrategy{<:Union{ElementAssembly, <:ElementAssemblyData}, <:AbstractSchedulingPolicy, <:AbstractCPUDevice},
    ::LinearIntegrator{<:NoStimulationProtocol},
    solver::AbstractSolver,
    dh::AbstractDofHandler,
)
    LinearNullOperator{value_type(strategy.device), ndofs(dh)}()
end
function setup_operator(
    strategy::AssemblyStrategy{<:FullAssembly, SequentialScheduling, <:AbstractGPUDevice},
    ::LinearIntegrator{<:NoStimulationProtocol},
    solver::AbstractSolver,
    dh::AbstractDofHandler,
)
    LinearNullOperator{value_type(strategy.device), ndofs(dh)}()
end
function setup_operator(
    strategy::AssemblyStrategy{<:FullAssembly, <:ColoredScheduling, <:AbstractGPUDevice},
    ::LinearIntegrator{<:NoStimulationProtocol},
    solver::AbstractSolver,
    dh::AbstractDofHandler,
)
    LinearNullOperator{value_type(strategy.device), ndofs(dh)}()
end
function setup_operator(
    strategy::AssemblyStrategy{<:Union{ElementAssembly, <:ElementAssemblyData}, <:AbstractSchedulingPolicy, <:AbstractGPUDevice},
    ::LinearIntegrator{<:NoStimulationProtocol},
    solver::AbstractSolver,
    dh::AbstractDofHandler,
)
    LinearNullOperator{value_type(strategy.device), ndofs(dh)}()
end

function setup_operator(
    strategy,
    integrator::AbstractLinearIntegrator,
    solver::AbstractSolver,
    dh::AbstractDofHandler,
)
    return setup_operator(strategy, integrator, dh)
end

# Bilinear
function setup_operator(
    strategy::Union{
        AssemblyStrategy{<:FullAssembly, SequentialScheduling, <:AbstractCPUDevice},
        AssemblyStrategy{<:FullAssembly, <:ColoredScheduling, <:AbstractCPUDevice},
    },
    integrator::AbstractBilinearIntegrator,
    solver::AbstractSolver,
    dh::AbstractDofHandler,
)
    setup_assembled_operator(strategy, integrator, solver.system_matrix_type, dh)
end
function setup_assembled_operator(
    strategy::AssemblyStrategy{<:FullAssembly, SequentialScheduling, <:AbstractCPUDevice},
    integrator::AbstractBilinearIntegrator,
    system_matrix_type::Type,
    dh::AbstractDofHandler,
)
    setup_operator(strategy, integrator, dh) # FIXME
end

# Nonlinear
"""
    setup_stage_operator(f, solver, local_solver_cache, t₀)

The operator the stage of `solver` solves with, for the function `f`.

Dispatches on the **pair**, because what is assembled and which dof handler it is assembled against
are both properties of the combination: a continuation poses the internal forces alone, backward Euler
poses them with a condensed local problem underneath, and Newmark poses them together with an inertia
term. Each method therefore names its own handler; nothing infers one from `f`.

Distinct from the `setup_operator(strategy, integrator, dh)` family, which materializes one integrator
against one handler and knows nothing about who will solve with it.

`local_solver_cache` is the per-quadrature-point scratch from [`setup_local_solver_cache`](@ref), or
`nothing` where nothing is condensed. `t₀` is the time any operator that has to be assembled once at
setup is assembled at.
"""
function setup_stage_operator end

function update_constraints!(
    f::AbstractSemidiscreteFunction,
    solver_cache::AbstractTimeSolverCache,
    t,
)
    Ferrite.update!(getch(f), t)
    apply!(solver_cache.uₙ, getch(f))
end

update_constraints!(f, solver_cache::AbstractTimeSolverCache, t) = nothing

function update_constraints!(
    f::AbstractSemidiscreteBlockedFunction,
    solver_cache::AbstractTimeSolverCache,
    t,
)
    for (i, pi) ∈ enumerate(blocks(f))
        update_constraints_block!(pi, Block(i), solver_cache, t)
    end
end

function update_constraints_block!(
    f::AbstractSemidiscreteFunction,
    i::Block,
    solver_cache::AbstractTimeSolverCache,
    t,
)
    Ferrite.update!(getch(f), t)
    u = @view solver_cache.uₙ[i]
    apply!(u, getch(f))
end

update_constraints_block!(
    f::SciMLBase.AbstractDiffEqFunction,
    i::Block,
    solver_cache::AbstractTimeSolverCache,
    t,
) = nothing

update_constraints_block!(f::NullFunction, i::Block, solver_cache::AbstractTimeSolverCache, t) =
    nothing


create_system_matrix(T::Type{<:AbstractMatrix}, f::AbstractSemidiscreteFunction) =
    create_system_matrix(T, f.dh)

function create_system_matrix(
    ::Type{<:ThreadedSparseMatrixCSR{Tv, Ti}},
    dh::AbstractDofHandler,
) where {Tv, Ti}
    Acsct = transpose(convert(SparseMatrixCSC{Tv, Ti}, allocate_matrix(dh)))
    return ThreadedSparseMatrixCSR(Acsct)
end

function create_system_matrix(SpMatType::Type{<:SparseMatrixCSC}, dh::AbstractDofHandler)
    A = convert(SpMatType, allocate_matrix(dh))
    return A
end

function create_system_vector(::Type{<:Vector{T}}, f::AbstractSemidiscreteFunction) where {T}
    return zeros(T, solution_size(f))
end

function create_system_vector(::Type{<:Vector{T}}, dh::DofHandler) where {T}
    return zeros(T, ndofs(dh))
end
